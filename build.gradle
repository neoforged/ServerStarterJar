import java.nio.file.Files

plugins {
    id 'java'
    id 'maven-publish'
    id 'me.modmuss50.mod-publish-plugin' version '0.5.1'
}

group = 'net.neoforged'

var describe = new ByteArrayOutputStream()
project.exec {
    commandLine('git', 'describe')
    standardOutput = describe
}
final splitVersion = describe.toString().trim().split('-')
if (splitVersion[0].split('\\.').length == 2) {
    println("Version: ${version = (splitVersion[0] + '.' + splitVersion[1])}")
} else {
    var splitVer = splitVersion[0].split('\\.')
    println("Version: ${version = (splitVer[0] + '.' + splitVer[1] + (splitVer[2] as int + splitVersion[1] as int))}")
}

base {
    archivesName = 'server'
}

sourceSets {
    java8
}

repositories {
    mavenCentral()
}

dependencies {
    implementation sourceSets.java8.output
    compileOnly 'org.jetbrains:annotations:24.1.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.assertj:assertj-core:3.27.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

tasks.named("compileJava8Java").configure {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

jar {
    from sourceSets.java8.output
    archiveFileName = 'server.jar'
    manifest.attributes([
            'Implementation-Version': project.version.toString(),
            'Main-Class'            : 'net.neoforged.serverstarterjar.Main8',
            'Premain-Class'         : 'net.neoforged.serverstarterjar.Agent',
            'Launcher-Agent-Class'  : 'net.neoforged.serverstarterjar.Agent'
    ])
}

tasks.register('updateServerJar') {
    dependsOn(tasks.jar)
    doFirst {
        var targetPath = rootProject.file('testing/server.jar').toPath()
        Files.deleteIfExists(targetPath)
        Files.copy(tasks.jar.archiveFile.get().asFile.toPath(), targetPath)
    }
}

var lastCommitMessage = new ByteArrayOutputStream()
project.exec {
    commandLine('git log -1 --pretty=%B'.split(' '))
    standardOutput = lastCommitMessage
}
publishMods {
    file = tasks.jar.archiveFile
    modLoaders.addAll('forge', 'neoforge')
    getChangelog().set(lastCommitMessage.toString().trim())
    type = STABLE
    github {
        accessToken = providers.environmentVariable('GITHUB_TOKEN')
        repository = 'neoforged/ServerStarterJar'
        commitish = 'main'
    }
}

tasks.register('productionTest', ProductionTest) {
    jar.set(tasks.jar.archiveFile)
    directory = project.layout.dir(project.provider { new File(project.projectDir, 'testing') })
}

abstract class ProductionTest extends DefaultTask {
    @InputFile
    abstract RegularFileProperty getJar()

    @Input
    @Option(option = 'neo-version', description = 'The version to test')
    abstract Property<String> getVersion()

    @OutputDirectory
    abstract DirectoryProperty getDirectory()

    @TaskAction
    void run() {
        var dir = directory.get().asFile.toPath()

        var version = getVersion().get()
        String installerUrl
        if (version.startsWith('forge-')) {
            installerUrl = "https://maven.minecraftforge.net/net/minecraftforge/forge/${version.replace('forge-', '')}/${version}-installer.jar"
        } else {
            installerUrl = "https://maven.neoforged.net/releases/net/neoforged/neoforge/${version}/neoforge-${version}-installer.jar"
        }

        Files.write(dir.resolve('installer.jar'), URI.create(installerUrl).toURL().openStream().readAllBytes())

        // TODO - accessing the project here is a bad idea
        project.javaexec {
            workingDir = dir.toFile()
            classpath(dir.resolve('installer.jar').toFile())
            args '--installServer'
        }.assertNormalExitValue()

        project.javaexec {
            workingDir = dir.toFile()
            classpath(jar)
        }.assertNormalExitValue()
    }
}
